 Init←{
     g←⍵
     t←g.Table
     t.Inverted←1=≢⍴t.Values
     n←≢t.Names
     ⍝ d←GetDimensions 0
     ⍝ Clean this mess up!!
     t.HeaderHeight←2.5×t.FontSize ⍝ d.thFontSize
     t.RowHeight←2×t.FontSize
     se←g.Document ##.GetElementById'data-grid-style'
     se.Document←g.Document
     _←se ##.SetProperty'textContent'((##.ComposeRules DynamicCSS t)~⎕UCS 13)
     ⍝ This should work: 'data-grid-style' A.SetProperty   ....
     ⍝ ↑↑↑↑↑
     t.DataSize←{t.Inverted:(≢0⊃⍵),≢⍵ ⋄ ⍴⍵}t.Values
     t.MaxLength←{t.Inverted:1⊃¨2↑¨⍴¨⍵ ⋄ ⌈⌿≢¨⍵}t.Values
     t.Widths←t ComputeWidths 0
     t.ColumnTitles←(t.RowNumbers/⊂''),GetColTitles¨t.Names
     t.ColumnOffset←t.RowNumbers+t.FreezeColumns
     t.WindowCell←t{2=⍺.⎕NC'WindowCell':⍺.WindowCell ⋄ ⍵}0,-t.FreezeColumns
     t.WindowIndex←t{2=⍺.⎕NC'WindowIndex':⍺.WindowIndex ⋄ ⍵}0,t.FreezeColumns
     t.DataCell←t.WindowIndex+t.WindowCell ⍝ Or (0 0)
     t.Selection←t.DataCell
     t.TableCell←t.WindowCell+0,t.ColumnOffset
     t.HeaderStyles←ComputeHeaderStyles t
     t.HeaderClass←ComputeHeaderClass t
     t.ColumnClass←ComputeColumnClass t
     t.AutoCompleteItems←n↑t.AutoCompleteItems,n⍴⊂''
     t.FMT←'FMT_'∘,¨t.Types
     t.VFI←'VFI_'∘,¨t.Types
     t.Highlight←{0≡⍵:⍵ ⋄ ⍵↑⍨-0 t.RowNumbers+⍴⍵}t.Highlight
     t.WindowPadding←3 5
     t.Init←1
     0
 }
