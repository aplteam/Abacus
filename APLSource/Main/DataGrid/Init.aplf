 Init←{
     g←⍵
     t←g.Table
     t.Inverted←1=≢⍴t.Values
     t.Columns←DefaultColumnProperties t
     ⍝ Always a mat.
     t.HeaderValues←{2=⍵.⎕NC'HeaderValues':⍵.HeaderValues ⋄ {(1,⍴⍵)⍴⍵}t.Columns.Name}t
     t.HeaderRowCount←≢t.HeaderValues
     t.Headers←DefaultHeaderProperties t
     n←≢t.Columns
     ⍝ For now: Total header height....
     t.HeaderHeight←(≢t.HeaderValues)×3×t.FontSize ⍝ Number of header rows ...
     t.RowHeight←2×t.FontSize
     _←g ##.SetDynamicCSS DynamicCSS t
     t.DataSize←{t.Inverted:(≢0⊃⍵),≢⍵ ⋄ ⍴⍵}t.Values
     t.MaxLength←{t.Inverted:1⊃¨2↑¨⍴¨⍵ ⋄ ⌈⌿≢¨⍵}t.Values
     t.Widths←t ComputeWidths 0
     ⍝ t.ColumnTitles←(t.RowNumbers/⊂''),GetColTitles¨t.Columns.Name
     t.ColumnTitles←(t.RowNumbers/t.Headers.Title),t.HeaderValues ⍝
     t.FixedColumns←t.RowNumbers+t.FreezeColumns
     t.WindowIndex←t{2=⍺.⎕NC'WindowIndex':⍺.WindowIndex ⋄ ⍵}0,t.FreezeColumns
     t.ActiveCell←t{2=⍺.⎕NC'ActiveCell':⍺.ActiveCell ⋄ ⍵}0 0
     t.Selection←2⍴⊂t.ActiveCell
     t.HeaderStyles←ComputeHeaderStyles t
     t.HeaderClass←ComputeHeaderClass t
     t.ColumnClass←((t.RowNumbers/'r'),t.Columns.Class),¨' ',¨'a',¨(t.RowNumbers/'r'),t.Columns.Align
     t.Highlight←{0≡⍵:⍵ ⋄ ⍵↑⍨-0 t.RowNumbers+⍴⍵}t.Highlight
     t.WindowPadding←3 5
     t.Init←1
     0
 }
