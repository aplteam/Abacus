 Init←{
     d g←⍺ ⍵
     t←g.Table
     t.Inverted←1=≢⍴t.Values
     t.DataSize←{t.Inverted:(≢0⊃⍵),≢⍵ ⋄ ⍴⍵}t.Values
     t.MaxLength←{t.Inverted:1⊃¨2↑¨⍴¨⍵ ⋄ ∊⌈⌿⍴¨⍵}t.Values
     t.Widths←t ComputeWidths 0
     t.FullWidths←t ComputeFullWidths t.Widths
     t.RowHeight←ComputeRowHeight t     ⍝ ComputeRowsToDisplay_old Resize
     t.ColumnTitles←(t.RowNumbers/⊂''),GetColTitles¨t.Names
     t.ColumnOffset←t.RowNumbers+t.FreezeColumns
     t.WindowCell←t{2=⍺.⎕NC'WindowCell':⍺.WindowCell ⋄ ⍵}0,-t.FreezeColumns
     t.WindowIndex←t{2=⍺.⎕NC'WindowIndex':⍺.WindowIndex ⋄ ⍵}0,t.FreezeColumns
     t.DataCell←t.WindowIndex+t.WindowCell ⍝ Or (0 0)
     t.Selection←t.DataCell
     t.TableCell←t.WindowCell+0,t.ColumnOffset
     t.HeaderStyles←ComputeHeaderStyles t
     t.HeaderClass←'sorted-down' '' 'sorted-up'[(t.RowNumbers/1),1+t.SortDirection]
     t.ColumnClass←(t.RowNumbers/⊂'row-number'),'' 'r'[t.Align∊⊂'right']
     t.FMT←'FMT_'∘,¨t.Types
     t.VFI←'VFI_'∘,¨t.Types
     t.Highlight←{0≡⍵:⍵ ⋄ ⍵↑⍨-0 t.RowNumbers+⍴⍵}t.Highlight
     t.Init←1
     0
 }
