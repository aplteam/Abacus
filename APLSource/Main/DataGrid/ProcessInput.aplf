 ProcessInput←{
     i←⍵
     t←i A.GetNearest'table'
     s←(1⊃t.DataCell)⊃t.Columns
     pfn←⍎s.OnParse
     b pv em←s pfn i.value
     ~b:i ShowDataEntryError em
     vfn←⍎s.OnValidate
     b pv em←s vfn pv
     ~b:i ShowDataEntryError em
     e←⎕NS''
     e.Event←'BeforeCellChange'
     e.CellIndex←t.DataCell
     e.OldValue←t GetValue t.DataCell
     e.NewValue←pv
     t Fire e:1
     fmt←⍎s.OnFormat
     fv←⊃s fmt⊂e.NewValue
     _←t SetValue e.NewValue
     t.OptiTable.Body.Values[⊂t.TableCell]←⊂fv
     c←i A.GetNearest'td'
     _←i A.RemoveAttribute'onblur'
     _←c A.SetInnerHTML fv
     _←c A.RemoveClass'in-cell'
     t.CellMode←0
     t.Changed←1
     e.Event←'CellChange'
     _←t Fire e
     0
 }
