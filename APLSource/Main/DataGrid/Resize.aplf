 Resize←{
     ⍝ ⍵ ←→ 0, use value of t.Init
     ⍝ ⍵ ←→ 1, Force Init
     g←⍺
     t←g.Table
     r←Init⍣(⍵∨~t.Init)⊣g
     r=1:0
     p←t.Parent
     0=p.⎕NC'Width':0
     t.AvailableWidth←p.Width-+/t.FixedColumns↑t.Widths
     t.ColumnsPerIndex←t.AvailableWidth ComputeColumnsPerIndex t.RowNumbers↓t.Widths
     0∊t.ColumnsPerIndex:0
     t.ScrollIntoViewIndex←ComputeScrollIntoViewIndex t.ColumnsPerIndex
     t.RowsToDisplay←⌊t.RowHeight÷⍨p.Height-t.HeaderHeight
     t.WindowSize←t.RowsToDisplay,(1⊃t.WindowIndex)⊃t.ColumnsPerIndex
     t.DataWidth←+/t.Widths
     t.DataHeight←t.RowHeight×⊃t.DataSize
     t.MaxWindowIndex←t.DataSize-t.WindowSize[0],1++/∧\2</⌽t.ColumnsPerIndex
     ∨/t.WindowSize<1:0
     t.TableCell←t ActiveCell2TableCell t.ActiveCell
     _←g ResizeScrollBars''
     _←BuildStructure t
     Refresh t
 }
