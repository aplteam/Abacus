 Resize←{
     ⍝ ⍵ ←→ 0, use value of t.Init
     ⍝ ⍵ ←→ 1, Force Init
     g←⍺
     t←g.Table
     _←Init⍣(⍵∨~t.Init)⊣g
     p←t.Parent
     0=p.⎕NC'Width':0
     t.AvailableWidth←p.Width-+/t.ColumnOffset↑t.Widths
     t.ColumnsPerIndex←t.AvailableWidth ComputeColumnsPerIndex t.RowNumbers↓t.Widths
     0∊t.ColumnsPerIndex:0
     t.ScrollIntoViewIndex←ComputeScrollIntoViewIndex t.ColumnsPerIndex
     t.RowsToDisplay←⌊t.RowHeight÷⍨p.Height-t.HeaderHeight
     t.WindowSize←t.RowsToDisplay,(1⊃t.WindowIndex)⊃t.ColumnsPerIndex
     t.DataWidth←+/t.Widths
     t.DataHeight←t.RowHeight×⊃t.DataSize
     t.MaxWindowIndex←t.DataSize-t.WindowSize[0],1++/∧\2</⌽t.ColumnsPerIndex
     ∨/t.WindowSize<1:0
     t.WindowCell←t.WindowCell⌊t.WindowSize-1
     t.DataCell←t.WindowIndex+t.WindowCell
     t.TableCell←t.WindowCell+0,t.ColumnOffset
     _←g ResizeScrollBars''
     _←BuildStructure t
     Refresh t
 }
