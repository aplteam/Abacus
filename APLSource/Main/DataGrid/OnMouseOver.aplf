 OnMouseOver←{
     t←⍵.CurrentTarget
     ~t.SelectInProgress:0
     c←⍵.Target
     c.Tag≢'td':0
     i←⊃⍸c=t.BodyCells
     d←t TableCell2DataCell i
     cr←(i[1]=t.ColumnOffset)∧(t.WindowIndex[1]≠0)∧(i[1]>t.DataCell[1])        ⍝ Crossing right
     cl←(i[1]=t.ColumnOffset-1)∧(t.WindowIndex[1]≠0)∧(i[1]<t.DataCell[1])      ⍝ On Border crossing left
     _←{
         cr:d[1]←t.FreezeColumns     ⍝ Crossing Border to right
         cl:d[1]←t.WindowIndex[1]-1  ⍝ Crossing to left
         0
     }0
     s←{(⌊/⍵),⌈/⍵}t.DataCell d
     s≡t.Selection:0
     t.Selection←s
     t.Selection≡t.DataCell:ClearSelection t
     _←d AdjustWindow t
     Refresh t
 }
