 GetContent←{
     t←⍺
     i j←t.WindowIndex+⍳¨(t.WindowSize+t.WindowPadding)⌊t.DataSize-t.WindowIndex
     k←(⍳⍺.FreezeColumns),j
     q←(⍺.RowNumbers/0),k+⍺.RowNumbers
     v←⍺ GetValues i k
     nr nc←⍴v
     o←⍺.OptiTable
     h←o.Header
     h.Values←(1,nc)⍴⍺.ColumnTitles[q]
     h.Cells.style←⍺.HeaderStyles[q]
     h.Cells.class←⍺.HeaderClass[q]
     h.Cells.id←nc↑t.HeaderCells.id
     o.Body.Values←v
     o.Body.Cells.id←(nr nc)↑t.BodyCells.id
     bc←t Highlight i q(nr nc⍴⍺.ColumnClass[q])
     bc←t Selection bc
     _←{
         ∨/t.TableCell<0:0
         bc[⊂t.TableCell]←⊂((⊂t.TableCell)⊃bc),' window-cell'
         0
     }0
     o.Body.Cells.class←bc
     ⍺.WindowIndex[1]≤⍺.FreezeColumns:o
     bc[;⍺.ColumnOffset]←bc[;⍺.ColumnOffset],¨⊂' more-to-left'
     o.Body.Cells.class←bc
     o
 }
