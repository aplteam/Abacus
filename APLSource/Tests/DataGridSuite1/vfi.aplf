 vfi←{
     ⍝ ⎕VFI using ⎕CSV
     ⍝ ⍵ ←→ Char Mat
     ⍝ ← ←→ Num vector
     w←{
         ~∨/'$%('∊⍵:⍵
         m←⍵
         m[⍸m[;0]='$';0]←' '
         lp←m[;0]='('
         ljust←{⍵⌽⍨+/∧\' '=⍵}
         m[;]←ljust⌽m
         rp←m[;0]=')'
         m[⍸m[;0]='%';0]←' '
         n←⍸lp∧rp
         m[n;0]←' '
         m[;]←ljust⌽m
         m[n;0]←'-'
         m
     }⍵
     w[⍸w∧.=' ';0]←'0'
     w←{~1∊'E+'⍷⍵:⍵ ⋄ ↑(↓⍵)~¨'+'}w ⍝ Kluge to handle fact that 'E+' not accepted by ⎕CSV!
     ⍺←-2*31
     o←⊂'Separator' '⎕'
     o,←⊂'Thousands' ','
     o,←⊂'Fill'⍺
     r←,(⎕CSV⍠o)(,w,⎕UCS 13)'S' 3
     v←r≠⍺
     v(v×r)
 }
