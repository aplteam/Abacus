 ReadCSVFile←{
     ⍝ ⍺ ←→ Limit rows, Limit Columns
     ⍝ ⍵ ←→ FQP file name
     ⍝ ← ←→ Namespace with Names, Values, etc.
     rl cl←2↑⍺,0
     o←'Invert' 1
     v n←(⎕CSV⍠o)⍵'' 1 1
     v←rl{⍺=0:⍵ ⋄ ⍺↑⍵}¨v
     n v←cl{⍺=0:⍵ ⋄ ⍺↑⍵}¨n v
     p←ConvertType¨v
     c←≢n
     t←⎕NS''
     t.Names←n
     t.Values←⍉↑{2=⍴⍴⍵:A.ntbs ⍵ ⋄ ⍵}¨p.Value
     ⍝ t.Values←p.Value
     t.Types←p.Type
     t.Align←p.Align
     t.SortDirection←c⍴0
     t.AutoCompleteItems←c⍴⊂''
     ~∨/n∊⊂'birthCity':t
     j←n⍳⊂'birthCity'
     t.AutoCompleteItems[j]←⊂∪t.Values[;j]
     j←n⍳⊂'birthCountry'
     t.AutoCompleteItems[j]←⊂∪t.Values[;j]
     j←n⍳⊂'birthState'
     t.AutoCompleteItems[j]←⊂∪t.Values[;j]
     t
 }
